/* ----------------------Preliminary Declarations Section--------------------*/

import absyn.*;import symb.SymbolTable;

/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:

    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {

        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");

        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }

        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);

        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }

    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

    /* Assume true at the start, but set to false with parsing errors */
    public static boolean valid = true;

:};


/* ------------Declaration of Terminals and Non Terminals Section----------- */

terminal IF, ELSE, INT, BOOL, VOID;
terminal RETURN, EQ, LT, GT, LEQ, GTE, NEQ, EEQ, PLUS, MINUS, AND, OR;
terminal LPAREN, RPAREN, LBRACK, RBRACK, LBRACE, RBRACE, SEMI, COMMA, NOT;
terminal String ID, NUM, TIMES, OVER, TRUTH, WHILE;

non terminal Exp program1;
non terminal DecList program;
non terminal ExpList stmt_seq;
non terminal ExpList args, arg_list;
non terminal Exp exp_stmt, stmt, return_stmt, iter_stmt, select_stmt, call;
non terminal Exp exp, obool_exp, abool_exp, ubool_exp, simple_exp, add_exp, term, signed_factor, factor, compound_stmt;
non terminal VarExp var;
non terminal Dec dec;
non terminal VarDec var_dec, var_dec_typ, param, param_typ;
non terminal VarDecList local_dec, param_list, params;
non terminal DecList dec_list;
non terminal FunctionDec fun_dec, fun_dec_typ, fun_pro, fun_pro_typ;
/* ----------------------------Grammar Section-------------------- */

start with program;


program1     ::= exp:e
                 {: RESULT = e; :}
               ;

//(1) <program> -> <declaration-list>
program	     ::= dec_list:s
                 {: new SymbolTable().showTable(s, 0);
                    RESULT = s; :}
               ;

//(2) <declaration-list> -> <declaration-list> <declaration> | <declaration>
dec_list    ::= dec_list:d1 dec:d2
                 {:
                    DecList l = d1;
                    while( l.tail != null )
                      l = l.tail;
                    l.tail = new DecList(d2, null);
                    RESULT = d1;
                 :}
               | dec:d
                 {: RESULT = new DecList(d, null); :}
               ;

//(3) <declaration> -> <var-declaration> | <fun-declaration> | <fun-prototype>
dec         ::= var_dec:v
                 {: RESULT = v; :}
               | fun_dec:f1
                 {: RESULT = f1; :}
               | fun_pro:f1
                 {: RESULT = f1; :}
            ;

//(4) <var-declaration> -> <type-specifier> ID ; | <type-specifier> ID [ NUM ] ;
var_dec      ::= INT:t var_dec_typ:v
                 {: v.type = new NameTy(tleft, NameTy.INT);
                   RESULT = v; :}
                 | BOOL:t var_dec_typ:v
                 {: v.type = new NameTy(tleft, NameTy.BOOL);
                   RESULT = v; :}
                 | VOID:t var_dec_typ:v
                 {: v.type = new NameTy(tleft, NameTy.VOID);
                   parser.report_error("row " + (vleft + 1) + ": Invalid Variable Decalartion: Type cannot be void", null);
                   RESULT = v; :}
                 ;
var_dec_typ  ::= ID:i SEMI
                 {: RESULT = new SimpleDec(0, null, i); :}
               | ID:i LBRACK NUM:n RBRACK SEMI
                 {: RESULT = new ArrayDec(0, null, i, Integer.parseInt(n)); :}
                 | error:e SEMI
                 {:
                   parser.report_error("row " + (eleft + 1) + ": Invalid Variable Decalartion", null);
                 :}
               ;
//(5) <type-specifier> -> bool | int | void

//(6) <fun-declaration> -> <type-specifier> ID ( <params> ) <compound-stmt>
fun_dec       ::= INT:i1 fun_dec_typ:f
                 {:  f.result = new NameTy(i1left, NameTy.INT);
                    RESULT = f; :}
               | BOOL:i1 fun_dec_typ:f
                 {:  f.result = new NameTy(i1left, NameTy.BOOL);
                    RESULT = f; :}
               | VOID:i1 fun_dec_typ:f
                 {:  f.result = new NameTy(i1left, NameTy.VOID);
                    RESULT = f; :}
                 | error:e LPAREN params:p RPAREN compound_stmt:c
                 {: parser.report_error("row " + (eleft + 1) + ": Invalid Function Declaration", e); :}
               ;
fun_dec_typ   ::= ID:i LPAREN params:p RPAREN compound_stmt:c
                 {: RESULT = new FunctionDec(ileft, null, i, p, c); :}
                  | error:e LPAREN params:p RPAREN compound_stmt:c
                  {: parser.report_error("row " + (eleft + 1) + ": Invalid Function Declaration", e); :}
                  | ID:i LPAREN error:e RPAREN compound_stmt:c
                  {: parser.report_error("row " + (eleft + 1) + ": Invalid Function Declaration", e); :}
                  | ID:i LPAREN params:p RPAREN error:e
                  {: parser.report_error("row " + (eleft + 1) + ": Invalid Function Declaration", e); :}
               ;

fun_pro       ::= INT:i fun_pro_typ:f
                {: f.result = new NameTy(ileft, NameTy.INT);
                   RESULT = f; :}
                | BOOL:i fun_pro_typ:f
               {: f.result = new NameTy(ileft, NameTy.BOOL);
                  RESULT = f; :}
                | VOID:i fun_pro_typ:f
               {: f.result = new NameTy(ileft, NameTy.VOID);
                  RESULT = f; :}
                 ;

//(7) <fun-prototype> -> <type-specifier> ID ( <params> ) ;
fun_pro_typ   ::= ID:i LPAREN params:p RPAREN SEMI
                 {: ;
                    RESULT = new FunctionDec(ileft, null, i, p, null); :}
               ;

//(8) <params> -> <param-list> | void
params        ::= param_list:p
                 {: RESULT = p; :}
                | VOID
                 {: RESULT = new VarDecList(null,null); :}
                ;

//(9) <param-list> -> <param-list> , <param> | <param>
param_list    ::= param_list:p1 COMMA param:p2
                 {: VarDecList l = p1;
                    while( l.tail != null )
                      l = l.tail;
                    l.tail = new VarDecList(p2, null);
                    RESULT = p1;
                 :}
                | param:p
                  {: RESULT = new VarDecList(p,null); :}
                 ;

//(10) <param> -> <type-specifier> ID | <type-specifier> ID [ ]
param         ::= INT:i param_typ:p
                 {: p.type = new NameTy(ileft, NameTy.INT);
                    p.pos = ileft;
                   RESULT = p; :}
               | BOOL:i param_typ:p
                 {: p.type = new NameTy(ileft, NameTy.BOOL);
                    p.pos = ileft;
                   RESULT = p; :}
               | VOID:i param_typ:p
                 {: p.type = new NameTy(ileft, NameTy.VOID);
                    p.pos = ileft;
                   RESULT = p; :}
               ;
param_typ     ::= ID:i
                 {: RESULT = new SimpleDec(0, null, i); :}
               | ID:i LBRACK RBRACK
                 {: RESULT = new ArrayDec(0, null, i, 0); :}
                ;

//(11) <compound-stmt> -> { <local-declarations> <statement-list> }
compound_stmt ::= LBRACE local_dec:l stmt_seq:s RBRACE
                 {: RESULT = new CompoundExp(lleft, l, s); :}
                ;

//(12) <local-declarations> -> <local-declarations> <var-declaration> |
local_dec    ::= /*empty*/
                 {: RESULT = new VarDecList(null, null); :}
               | local_dec:d var_dec:v1
                 {: VarDecList l = d;
                    if (l.head == null){
                      l.head = v1;
                    } else {
                        while( l.tail != null )
                          l = l.tail;
                        l.tail = new VarDecList(v1, null);
                    }
                    RESULT = d;
                 :}
                 ;

//(13) <statement-list> -> <statement-list> <statement> |
stmt_seq     ::= stmt_seq:s1 stmt:s2
                 {: ExpList t = s1;
                    if (t.head == null) {
                      t.head = s2;
                    } else {
                        while( t.tail != null )
                          t = t.tail;
                        t.tail = new ExpList(s2, null);
                    }
                    RESULT = s1;
                 :}
               | /*empty*/
                 {: RESULT = new ExpList(new NilExp(0), null); :}
               ;
//(14) <statement> -> <expression-stmt> | <compound-stmt> | <selection-stmt> | <iteration-stmt> | <return-stmt>
stmt         ::=  exp_stmt:e
                 {: RESULT = e; :}
               | compound_stmt:c
                 {: RESULT = c; :}
               | iter_stmt:i
                 {: RESULT = i; :}
               | select_stmt:s
                 {: RESULT = s; :}
               | return_stmt:r
                 {: RESULT = r; :}
               | error:e
                 {: parser.report_error("row " + (eleft + 1) + ": invalid statement", null); :}
               ;

//(15) <expression-stmt> -> <expression> ; | ;
exp_stmt     ::= exp:e SEMI
                 {: RESULT = e; :}
               | SEMI
                 {: RESULT = new NilExp(0); :}
               ;

//(16) <selection-stmt> -> if ( <expression> ) <statement> | if ( <expression> ) <statement> else <statement>
select_stmt ::= IF:i LPAREN exp:e RPAREN stmt:s1 ELSE stmt:s2
                 {: RESULT = new IfExp(ileft, e, s1, s2); :}
                | IF:i LPAREN exp:e RPAREN stmt:s
                 {: RESULT = new IfExp(ileft, e, s, new NilExp(0)); :}
               ;

//(17) <iteration-stmt> -> while ( <expression> ) <statement>
iter_stmt  ::= WHILE:w LPAREN exp:e RPAREN stmt:s
                 {: RESULT  = new WhileExp(wleft, e, s); :}
              ;

//(18) <return-stmt> -> return <expression> ; | return ;
return_stmt ::= RETURN:r exp:e SEMI
                 {: RESULT = new ReturnExp(rleft, e); :}
              | RETURN:r SEMI
                 {: RESULT = new ReturnExp(rleft, new NilExp(0)); :}
              ;

//(19) <expression> -> <var> = <expression> | <obool-expression>
exp           ::= var:v1 EQ exp:e2
                 {: RESULT =  new AssignExp(v1left, v1, e2); :}
               | obool_exp: e
                 {: RESULT = e; :}
              ;

//(20) <var> -> ID | ID [ <expression> ]
var          ::= ID:i
                 {: RESULT = SimpleVar.var(ileft, i); :}
               | ID:i1 LBRACK exp:e2 RBRACK
                 {: RESULT = IndexVar.var(i1left, i1, e2); :}
             ;

//(21) <obool-expression> -> <obool-expression> || <abool-expression> | <abool-expression>
obool_exp      ::= obool_exp:o1 OR abool_exp:a2
                 {: RESULT = new OpExp(o1left, o1, OpExp.OR, a2); :}
               |   abool_exp:a
                 {: RESULT = a; :}
               ;

//(22) <abool-expression> -> <abool-expression> && <ubool-expression> | <ubool-expression>
abool_exp      ::= abool_exp:a AND ubool_exp:u
                 {: RESULT = new OpExp(aleft, a, OpExp.AND, u); :}
               |   ubool_exp:u
                 {: RESULT = u; :}
               ;

//(23) <ubool-expression> -> ~ <ubool-expression> | <simple-expression>
ubool_exp      ::= NOT ubool_exp:u
                 {: RESULT = new OpExp(uleft, new NilExp(0), OpExp.NOT, u); :}
               | simple_exp:s
                 {: RESULT = s; :}
               ;

//(24) <simple-expression> -> <additive-expression> <relop> <additive-expression> | <additive-expression>
simple_exp          ::= add_exp:s1 LT add_exp:s2
                 {: RESULT = new OpExp(s1left, s1, OpExp.LT, s2); :}
               | add_exp:s1 GT add_exp:s2
                 {: RESULT = new OpExp(s1left, s1, OpExp.GT, s2); :}
               | add_exp:s1 NEQ add_exp:s2
                 {: RESULT = new OpExp(s1left, s1, OpExp.NEQ, s2); :}
               | add_exp:s1 GTE add_exp:s2
                 {: RESULT = new OpExp(s1left, s1, OpExp.GTE, s2); :}
               | add_exp:s1 LEQ add_exp:s2
                 {: RESULT = new OpExp(s1left, s1, OpExp.LEQ, s2); :}
               | add_exp:s1 EEQ add_exp:s2
                 {: RESULT = new OpExp(s1left, s1, OpExp.EEQ, s2); :}
               | add_exp:s
                 {: RESULT = s; :}
               ;

//(25) <relop> -> <= | < | > | >= | == | !=

//(26) <additive-expression> -> <additive-expression> <addop> <term> | <term>
add_exp   ::= add_exp:s PLUS term:t
                 {: RESULT = new OpExp(sleft, s, OpExp.PLUS, t); :}
               | add_exp:s MINUS term:t
                 {: RESULT = new OpExp(sleft, s, OpExp.MINUS, t); :}
               | term:t
                 {: RESULT = t; :}
               ;

//(27) <addop> -> + | -

//(28) <term> -> <term> <mulop> <signed-factor> | <signed-factor>
term         ::= term:t TIMES signed_factor:f
                 {: RESULT = new OpExp(tleft, t, OpExp.TIMES, f); :}
               | term:t OVER signed_factor:f
                 {: RESULT = new OpExp(tleft, t, OpExp.OVER, f); :}
               | signed_factor:f
                 {: RESULT = f; :}
               ;

//(29) <mulop> -> * | /

//(30) <signed-factor> -> - <signed-factor> | <factor>
signed_factor ::= MINUS:m signed_factor:f
                 {: RESULT = new OpExp(mleft, new NilExp(0), OpExp.UMINUS, f); :}
               |  factor:f
                 {: RESULT = f; :}
               ;

//(31) <factor> -> ( <expression> ) | <var> | <call> | NUM | TRUTH
factor       ::= LPAREN exp:e RPAREN
                 {: RESULT = e; :}
               | NUM:n
                 {: RESULT = new IntExp(nleft, n); :}
               | var:v
                 {: RESULT = v; :}
               | TRUTH:t
                 {: RESULT = new BoolExp(tleft, t); :}
               | call:c
                 {: RESULT = c; :}
               | error RPAREN:e
                 {: parser.report_error("row " + (eleft + 1) + ": unmatched right parenthese in factor", null);
                    RESULT = new NilExp(0);
                 :}
               | error LPAREN:e
                 {: parser.report_error("row " + (eleft + 1) + ": unmatched left parenthese in factor", null);
                    RESULT = new NilExp(0);
                 :}
               ;

//(32) <call> -> ID ( <args> )
call         ::= ID:i1 LPAREN args:a2 RPAREN
                 {: RESULT = new CallExp(i1left, i1, a2); :}
                 ;

//(33) <args> -> <arg-list> |
args         ::= arg_list:a
                 {: RESULT = a; :}
                | /*empty*/
                 {: RESULT = new ExpList(new NilExp(0), null); :}
                | arg_list:a COMMA
                 {: parser.report_error("row " + (aleft + 1) + ": unmatched comment in argument list", null);
                    RESULT = a;
                 :}
                ;

//(34) <arg-list> -> <arg-list> , <expression> | <expression>
arg_list     ::= arg_list:a1 COMMA exp:e2
                 {: ExpList t = a1;
                    while( t.tail != null )
                      t = t.tail;
                    t.tail =  new ExpList(e2, null);
                    RESULT = a1;
                 :}
               | exp:e
                 {: RESULT = new ExpList(e, null); :}
               ;